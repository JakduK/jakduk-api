plugins {
    id 'org.springframework.boot' version '2.4.13'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
}

group = 'com.jakduk'
version = '1.0.0'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-mail')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
    implementation('org.springframework.boot:spring-boot-starter-data-elasticsearch')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // etc libs
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.github.dfabulich:sitemapgen4j:1.1.2'
    implementation 'com.rometools:rome:1.18.0'
    implementation 'net.coobird:thumbnailator:0.4.17'
    implementation 'org.jsoup:jsoup:1.14.3'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
//    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo' this version does not support 32Bit: Version{3.5.5}:OS_X:B32 에러 남
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation('org.springframework.security:spring-security-test')
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()

    testLogging { exceptionFormat = 'full' }
}

asciidoctor {
    inputs.dir snippetsDir
    sourceDir 'src/main/asciidoc'
    attributes 'snippets': file(snippetsDir)
    dependsOn test
}

bootJar {
    // gradle build 시 publishDocs 파라미터가 들어와야만 동작 함
    if (project.hasProperty('publishDocs')) {
        // build/asciidoc/html5 에 있는 documentation 을 jar file 로 package 하기 위함
        from("${asciidoctor.outputDir}/html5") {
            into 'public/docs'
        }
    }

    launchScript()
}